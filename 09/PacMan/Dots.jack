class Dots {
  field Array dots;
  field int nDots;
  field int perRow;
  field int rows;
  field int nCaptured;

  constructor Dots new() {
    let perRow = 22;
    let rows = 6;
    let nDots = perRow * rows;
    let dots = Array.new(nDots);
    do createDots();
    return this;
  }

  method void init() {
    do draw();
    return;
  }

  method void createDots() {
    var int x, y;
    var int offsetX, offsetY;
    var int i;
    var int row, col;
    let offsetX = 4;
    let offsetY = 16;
    let i = 0;

    while (i < nDots) {
      // TODO: create with formula
      let row = i / perRow;
      let col = i - (row * perRow);
      let x = offsetX + (2*col) + (20 * (col+1));
      let y = offsetY + (2*row) + (32 * (row+1));
      let dots[i] = Dot.new(x, y);
      let i = i + 1;
    }
    return;
  }

  method void draw() {
    var int i;
    var Dot cur;
    let i = 0;
    while (i < nDots) {
      let cur = dots[i];
      do cur.draw();
      let i = i + 1;
    }
    return;
  }

  method int getNCaptured() {
    return nCaptured;
  }

  method void setCaptured(int x1, int y1, int x2, int y2) {
    var int i;
    var Dot cur;
    let i = 0;
    while (i < nDots) {
      let cur = dots[i];
      if (cur.isOverlap(x1, y1, x2, y2)) {
        if (~(cur.getIsCaptured())) {
          do cur.setCaptured();
          do cur.draw();
          let nCaptured = nCaptured + 1;
        }
        return;
      }
      let i = i + 1;
    }
    return;
  }

  /** Disposes this Dots. */
  method void dispose() {
    do Memory.deAlloc(this);
    return;
  }
}